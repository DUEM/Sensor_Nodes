// Script for using a sensor node as a serial to CAN interface.
// Only relies on the MCP2515 CAN library, not any of the DUEM code
// To use, either wire up an arduino to aN MCP2515/2551 combo, or plug a serial adapter into a Sensornode.
// If the MCP2515 has been set-up okay you will see "CAN BUS Shield init ok!" (if you see this message on a sensor node then god help you)
// Messages should appear on the serial, just beware that the MSB might be the first byte!
// To send a message, type comma separated integers (e.g. 1,2,3,76,245) followed by a newline
// First integer is the message ID to send as,
// Second integer is the length of the message in bytes (0-8)
// Next integers are the values to send for each byte (0-255)
// Example: 1024,4,1,2,3,4 should send Msg_id: #400, Msg_data: 01 02 03 04
// BEWARE: I think the byte order gets sent the wrong way round so the MSB is on the far left.
// You'll have to use MAGIC(tm) to fix this (or just rewrite some code)

// This is also a good example of how to use the basic Seeedstudio MCP2515 CAN library.

#include <SPI.h>
#include "mcp_can.h"

unsigned char Flag_Recv = 0;

INT32U in_id = 0;
INT8U in_len = 0;
INT8U in_data[8];

INT32U out_id = 0;
INT8U out_len = 0;
INT8U out_data[8];

char str[20];


MCP_CAN CAN(10);                                            // Set CS to pin 10

void setup()
{
    Serial.begin(115200);

START_INIT:

    if(CAN_OK == CAN.begin(CAN_500KBPS))                   // init can bus : baudrate = 500k
    {
        Serial.println("CAN BUS Shield init ok!");
    }
    else
    {
        Serial.println("CAN BUS Shield init fail");
        Serial.println("Init CAN BUS Shield again");
        delay(100);
        goto START_INIT;
    }
}


void loop()
{
    if(CAN_MSGAVAIL == CAN.checkReceive())            // check if data coming
    {
        CAN.readMsgBufID(&in_id, &in_len, in_data);    // read data,  len: data length, buf: data buf
        
        Serial.print(">>> id: "); Serial.print(in_id); Serial.print(", data:");
        for(int i = 0; i < in_len; i++) { Serial.print(" "); Serial.print(in_data[i],HEX);}
        Serial.println();
    }
    
    
    while (Serial.available() > 0) {
      
        // look for the next valid integer in the incoming serial stream:
        out_id = Serial.parseInt();
        out_len = Serial.parseInt();
        
        if(out_len<0) out_len=0;
        if(out_len>8) out_len=8;
        
        for (int i=0; i < out_len; i++) { out_data[i] = Serial.parseInt(); }
    

        if (Serial.read() == '\n') {
            
            CAN.sendMsgBuf(out_id, 0, out_len, out_data);
            
            
            Serial.print("<<< id: "); Serial.print(out_id); Serial.print(", data:");
            for(int i = 0; i < out_len; i++) { Serial.print(" "); Serial.print(out_data[i],HEX);}
            Serial.println();
            
        }
    }
}
